plugins {
    id("com.gtnewhorizons.gtnhconvention")
}

minecraft {
    extraRunJvmArguments.addAll("-Xmx8G", "-Xms8G", "-Dgtnhlib.dumpkeys=true")
}

val functionalStorageVersion = "0.1.0"

version = functionalStorageVersion

tasks.withType<ProcessResources>().configureEach {
    inputs.property("functionalStorageVersion", functionalStorageVersion)
    filesMatching("mcmod.info") {
        expand(
            "modId" to project.property("modId"),
            "modName" to project.property("modName"),
            "modVersion" to functionalStorageVersion,
            "minecraftVersion" to project.property("minecraftVersion")
        )
    }
}

tasks.withType<JavaCompile>().configureEach {
    options.annotationProcessorPath = configurations.annotationProcessor.get()
}

tasks.withType<Test>().configureEach {
    useJUnitPlatform()
}

val runConfigs = listOf(
    "runClient" to "run/client",
    "runClient17" to "run/client_new",
    "runClient21" to "run/client_new",
    "runClient25" to "run/client_new",
    "runServer" to "run/server",
    "runServer17" to "run/server_new",
    "runServer21" to "run/server_new",
    "runServer25" to "run/server_new"
)

runConfigs.forEach { (taskName, path) ->
    tasks.named<JavaExec>(taskName) {
        workingDir = file("${projectDir}/$path")
        doFirst {
            workingDir.mkdirs()
        }
    }
}
